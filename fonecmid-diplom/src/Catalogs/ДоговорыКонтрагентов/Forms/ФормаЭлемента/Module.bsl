#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// ++Красова Задача №1 , 20.01.2025
	ГруппаАбонентскоеОбслуживание = Элементы.Вставить("АбонентскоеОбслуживание", Тип("ГруппаФормы"));
	ГруппаАбонентскоеОбслуживание.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаАбонентскоеОбслуживание.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
	ГруппаАбонентскоеОбслуживание.Заголовок = "Абонентское обслуживание"; 
	ГруппаАбонентскоеОбслуживание.ОтображатьЗаголовок = Ложь;
	ГруппаАбонентскоеОбслуживание.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаАбонентскоеОбслуживание.Видимость = Объект.ВидДоговора = 
	ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание");
	
	ГруппаПериодДоговора = Элементы.Добавить("ПериодДоговора", Тип("ГруппаФормы"), ГруппаАбонентскоеОбслуживание);
	ГруппаПериодДоговора.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаПериодДоговора.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаПериодДоговора.Заголовок = "ПериодДоговора"; 
	ГруппаПериодДоговора.ОтображатьЗаголовок = Ложь;
	ГруппаПериодДоговора.Отображение = ОтображениеОбычнойГруппы.Нет;
	
	ЭлементПериодДействияДоговора = Элементы.Добавить("ВКМ_ДатаНачалаДействияДоговора", Тип("ПолеФормы"), ГруппаПериодДоговора);
	ЭлементПериодДействияДоговора.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементПериодДействияДоговора.ПутьКДанным = "Объект.ВКМ_ДатаНачалаДействияДоговора"; 
	
	ЭлементПериодДействияДоговора = Элементы.Добавить("ВКМ_ДатаОкончанияДействияДоговора", Тип("ПолеФормы"), ГруппаПериодДоговора);
	ЭлементПериодДействияДоговора.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементПериодДействияДоговора.ПутьКДанным = "Объект.ВКМ_ДатаОкончанияДействияДоговора";
	
	КомандаВыборПериода = Команды.Добавить("ВыборПериода");
	КомандаВыборПериода.Заголовок = "Выбор периода";
	КомандаВыборПериода.Действие = "ВыборПериода"; 
	
	КнопкаВыборПериода = Элементы.Добавить("ВыборПериода", Тип("КнопкаФормы"), ГруппаПериодДоговора);
	КнопкаВыборПериода.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаВыборПериода.ИмяКоманды = "ВыборПериода";
	КнопкаВыборПериода.Картинка = БиблиотекаКартинок.Выбрать;
	КнопкаВыборПериода.Отображение = ОтображениеКнопки.Текст;
	
	ЭлементСуммаАбонентскойПлаты = Элементы.Добавить("ВКМ_СуммаАбонентскойПлаты", Тип("ПолеФормы"), ГруппаАбонентскоеОбслуживание);
	ЭлементСуммаАбонентскойПлаты.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСуммаАбонентскойПлаты.ПутьКДанным = "Объект.ВКМ_СуммаАбонентскойПлаты";
	
	ЭлементСтоимостьЧасаРаботы = Элементы.Добавить("ВКМ_СтоимостьЧасаРаботы", Тип("ПолеФормы"), ГруппаАбонентскоеОбслуживание);
	ЭлементСтоимостьЧасаРаботы.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСтоимостьЧасаРаботы.ПутьКДанным = "Объект.ВКМ_СтоимостьЧасаРаботы";
	// -- 
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы 

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)
	// ++Красова Задача №1 , 20.01.2025
	Элементы.АбонентскоеОбслуживание.Видимость = Объект.ВидДоговора = 
	ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание");
	// --
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы
	
&НаКлиенте
Процедура ВыборПериода(Команда)
	// ++Красова Задача №1 , 20.01.2025 
	 Диалог = Новый ДиалогРедактированияСтандартногоПериода();                                 
	 Диалог.Период = Новый СтандартныйПериод(Объект.ВКМ_ДатаНачалаДействияДоговора, Объект.ВКМ_ДатаОкончанияДействияДоговора);        
	 ОписаниеОповещенияОЗакрытииФормы = Новый ОписаниеОповещения("ВыполнитьПослеВыбораПериода", ЭтотОбъект);
	 
	 Диалог.Показать(ОписаниеОповещенияОЗакрытииФормы);
	 // --  
КонецПроцедуры
 
 &НаКлиенте
Процедура ВыполнитьПослеВыбораПериода(Результат, Параметры) Экспорт
	// ++Красова Задача №1 , 20.01.2025
	Если Результат <> Неопределено Тогда
			
		Объект.ВКМ_ДатаНачалаДействияДоговора = Результат.ДатаНачала;
	    Объект.ВКМ_ДатаОкончанияДействияДоговора = Результат.ДатаОкончания; 	
				
	КонецЕсли;
	// -- 
КонецПроцедуры

#КонецОбласти 
	
